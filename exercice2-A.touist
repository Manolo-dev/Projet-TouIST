$C=[vert, orange, bleu, rouge, rose, vide]

$k=7
$ETAPES=[0..$k]

p(0, 1, 1, vert) ;; the first tube, first layer is green
p(0, 1, 2, orange) ;; the first tube, second layer is orange
p(0, 1, 3, orange) ;; the first tube, third layer is green
p(0, 1, 4, vert) ;; the first tube, fourth layer is orange

;; end init of the first tube
;; so it's look like this
;; tube 1
;; |  vert  |
;; | orange |
;; | orange |
;; |  vert  |
;; \________/

;; 2 is for the second tube

p(0, 2, 1, orange) ;; the second tube, first layer is orange
p(0, 2, 2, vert) ;; the second tube, second layer is green
p(0, 2, 3, vert) ;; the second tube, third layer is orange
p(0, 2, 4, orange) ;; the second tube, fourth layer is green

;; end init of the second tube
;; so it's look like this
;; tube 2
;; | orange |
;; |  vert  |
;; |  vert  |
;; | orange |
;; \________/

;; 3 is for the third tube
p(0, 3, 1, vide) ;; the third tube, first layer is empty
p(0, 3, 2, vide) ;; the third tube, second layer is empty
p(0, 3, 3, vide) ;; the third tube, third layer is empty
p(0, 3, 4, vide) ;; the third tube, fourth layer is empty

;; end init of the third tube
;; so it's look like this
;; tube 3
;; |        |
;; |        |
;; |        |
;; |        |
;; \________/

;; p(étape, tube, étage, couleur)

;; first rule
bigand $i in $ETAPES: ;; for each state
    bigand $t in [1..3]: ;; for each tube
        bigand $e in [1..4]: ;; for each layer
            bigor $c in $C: ;; at least one color
                p($i, $t, $e, $c) ;; every state, every tube, every layer, have one color at minimum
            end
        end
    end
end

;; second rule
bigand $i in $ETAPES: ;; for each state
    bigand $t in [1..3]: ;; for each tube
        bigand $e in [1..4]: ;; for each layer
            bigand $c in $C: ;; for each color
                bigand $c2 in $C when $c2 != $c: ;; for each color different from the previous one
                    not p($i, $t, $e, $c) or not p($i, $t, $e, $c2) ;; if a state, a tube, a layer have a color, it can't have another color
                end
            end
        end
    end
end

;; third rule
bigand $i in $ETAPES: ;; for each state
    bigand $t in [1..3]: ;; for each tube
        bigand $e in [1..3]: ;; for each layer
            p($i, $t, $e, vide) => p($i, $t, $e+1, vide) ;; if a state, a tube, a layer is empty, then the layer above it is empty as well
        end
    end
end

;; end result
bigand $t in [1..3]: ;; for each tube
    bigor $c in $C: ;; at least one color
        bigand $e in [1..4]: ;; for each layer
            p($k, $t, $e, $c) ;; the tube is full of a single color
        end
    end
end

bigand $t in [1..3]: ;; for each tube
    bigand $c in $C: ;; for each color
        bigand $c1 in $C when $c != $c1: ;; for each color
            bigand $e in [1..4]: ;; for each layer 
                not p($k, $t, $e, $c) or not p($k, $t, $e, $c1) ;; the tube have only one color
            end
        end
    end
end

;; rules 2
;; 
;; verser(étape, tube, étage, tube2, étage2, couleur)
bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C when $c != vide: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => p($e-1, $t, $e1, $c) ;; the tube had a color in that layer before pouring
                    end
                end
            end
        end
    end
end

bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C when $c != vide: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => p($e-1, $t2, $e2, vide) ;; the target layer in the target tube is empty
                    end
                end
            end
        end
    end
end

bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [2..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [2..4]: ;; for each layer
                    bigand $c in $C: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => (p($e-1, $t2, $e2-1, $c)) ;; the target layer is above a layer of similar color
                    end
                end
            end
        end
    end
end


;; rules 3
;; bigand $e in $ETAPES: ;; for each step
;;     bigand $t in [1..3]: ;; for each tube
;;         bigand $e1 in [1..4]: ;; for each layer
;;             bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
;;                 bigand $e2 in [1..4]: ;; for each layer
;;                     bigand $c in $C: ;; for each color
;;                         verser($e, $t, $e1, $t2, $e2, $c) => p($e, $t, $e1, vide)
;;                     end
;;                 end
;;             end
;;         end
;;     end
;; end

;; bigand $e in $ETAPES: ;; for each step
;;     bigand $t in [1..3]: ;; for each tube
;;         bigand $e1 in [1..4]: ;; for each layer
;;             bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
;;                 bigand $e2 in [1..4]: ;; for each layer
;;                     bigand $c in $C: ;; for each color
;;                         verser($e, $t, $e1, $t2, $e2, $c) => p($e, $t2, $e2, $c)
;;                     end 
;;                 end
;;             end
;;         end
;;     end
;; end

bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => (p($e, $t, $e1, vide) and p($e, $t2, $e2, $c))
                    end
                end
            end
        end
    end
end

;; rules 4
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C when $c != vide: ;; for each color
                (p($e-1, $t, $e1, vide) and p($e, $t, $e1, $c)) => ;; if the tube is empty and after the color is here
                    bigor $t2 in [1..3] when $t != $t2: ;; for at least one other tube
                        bigor $e2 in [1..4]: ;; for at least one layer
                            verser($e, $t2, $e2, $t, $e1, $c) ;; then we can move the color
                        end
                    end
            end
        end
    end
end

;; rules 5
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C when $c != vide: ;; for each color
                (p($e-1, $t, $e1, $c) and p($e, $t, $e1, vide)) => ;; if the tube is empty and after the color is here
                    bigor $t2 in [1..3] when $t != $t2: ;; for at least one other tube
                        bigor $e2 in [1..4]: ;; for at least one layer
                            verser($e, $t, $e1, $t2, $e2, $c) ;; then we can move the color
                        end
                    end
            end
        end
    end
end

;; rules 6
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C when $c != vide: ;; for each color
                p($e-1, $t, $e1, $c) => ;; if the tube have a color
                    (p($e, $t, $e1, vide) or p($e, $t, $e1, $c)) ;; after each step, any filled layer is either empty or has the same color as before
            end
        end
    end
end

;; rules 7
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C: ;; for each color
                bigand $t2 in [1..3]: ;; for at least one other  tube (target tube)
                    bigand $e2 in [1..4]: ;; for at least one other layer (target layer)
                        bigand $t3  in [1..3]: ;; for at least one other tube (source tube)
                            bigand $e3 in [1..4]: ;; for at least one other layer (source layer)
                                bigand $t4 in [1..3]: ;; for at least one other tube (source tube)
                                    bigand $e4 in [1..4]: ;; for at least one other layer (source layer)
                                        bigand $c2 in $C when $c2 != $c or $t != $t3 or $e1 != $e3 or $t2 != $t4 or $e2 != $e4: ;; for each color
                                            verser($e, $t, $e1, $t2, $e2, $c) => not verser($e, $t3, $e3, $t4, $e4, $c2) ;; no other pour can be true
                                        end
                                    end
                                end
                            end
                        end
                    end
                end
            end
        end
    end
end

;; max layer
;; bigand $e in $ETAPES: ;; for each step
;;     bigand $t in [1..3]: ;; for each tube
;;             bigand $c in $C: ;; for each color
;;                 not p($e, $t, 0, $c) ;; no color can be in the 0th layer
;;             end
;;     end
;; end