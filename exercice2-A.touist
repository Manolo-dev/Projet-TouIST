$C=[vert, orange, bleu, rouge, rose, vide]

$k=1
$ETAPES=[0..$k]

p(0, 1, 1, vide)
p(0, 1, 2, vide)
p(1, 1, 1, orange)

;; p(étape, tube, étage, couleur)

;; end result
bigand $t in [1..3]: ;; for each tube
    bigor $c in $C: ;; at least one color
        bigand $e in [1..4]: ;; for each layer
            p($k, $t, $e, $c) ;; the tube is full of a single color
        end
    end
end

bigand $t in [1..3]: ;; for each tube
    bigand $c in $C: ;; for each color
        bigand $c1 in $C when $c != $c1: ;; for each color
            bigand $e in [1..4]: ;; for each layer 
                not p($k, $t, $e, $c) or not p($k, $t, $e, $c1) ;; the tube have only one color
            end
        end
    end
end

;; rules 2
;; 
;; verser(étape, tube, étage, tube2, étage2, couleur)
bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C when $c != vide: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => p($e-1, $t, $e1, $c) ;; the tube had a color in that layer before pouring
                    end
                end
            end
        end
    end
end

bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C when $c != vide: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => p($e-1, $t2, $e2, vide) ;; the target layer in the target tube is empty
                    end
                end
            end
        end
    end
end

bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [2..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => (p($e, $t2, $e2-1, $c)) ;; the target layer is above a layer of similar color
                    end
                end
            end
        end
    end
end

;; rules 3
bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => p($e, $t, $e1, vide)
                    end
                end
            end
        end
    end
end

bigand $e in $ETAPES: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $t2 in [1..3] when $t != $t2: ;; for each other tube
                bigand $e2 in [1..4]: ;; for each layer
                    bigand $c in $C: ;; for each color
                        verser($e, $t, $e1, $t2, $e2, $c) => p($e, $t2, $e2, $c)
                    end 
                end
            end
        end
    end
end

;; rules 4
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C when $c != vide: ;; for each color
                (p($e-1, $t, $e1, vide) and p($e, $t, $e1, $c)) => ;; if the tube is empty and after the color is here
                    bigor $t2 in [1..3] when $t != $t2: ;; for at least one other tube
                        bigor $e2 in [1..4]: ;; for at least one layer
                            verser($e, $t2, $e2, $t, $e1, $c) ;; then we can move the color
                        end
                    end
            end
        end
    end
end

;; rules 5
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C when $c != vide: ;; for each color
                (p($e-1, $t, $e1, $c) and p($e, $t, $e1, vide)) => ;; if the tube is empty and after the color is here
                    bigor $t2 in [1..3] when $t != $t2: ;; for at least one other tube
                        bigor $e2 in [1..4]: ;; for at least one layer
                            verser($e, $t, $e1, $t2, $e2, $c) ;; then we can move the color
                        end
                    end
            end
        end
    end
end

;; rules 6
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C when $c != vide: ;; for each color
                p($e-1, $t, $e1, $c) => ;; if the tube have a color
                    (p($e, $t, $e1, vide) or p($e, $t, $e1, $c)) ;; after each step, any filled layer is either empty or has the same color as before
            end
        end
    end
end

;; rules 7
bigand $e in $ETAPES when $e != 0: ;; for each step
    bigand $t in [1..3]: ;; for each tube
        bigand $e1 in [1..4]: ;; for each layer
            bigand $c in $C: ;; for each color
                bigor $t2 in [1..3]: ;; for at least one other  tube (target tube)
                    bigor $e2 in [1..4]: ;; for at least one layer
                        verser($e, $t, $e1, $t2, $e2, $c) => ;; only one verser by etape
                            bigand $t3  in [1..3] when $t3 !=$t : ;; for each different tube
                                bigand $e3 in [1..4] when $e3 !=$e1: ;; for each layer
                                    bigand $t4 in [1..3] when $t4 != $t2: ;; for each different target tube
                                        bigand $e4 in [1..4] when $e4 !=$e2: ;; for each layer
                                            bigand $c2 in $C when $c2 != $c: ;; for each different color
                                                not verser($e, $t3, $e3, $t4, $e4, $c2) ;; no other verser
                                            end
                                        end
                                    end
                                end
                            end
                    end
                end
            end
        end
    end
end